let filename = "gitlab-pages/docs/tutorials/get-started/tezos-taco-shop-smart-contract.jsligo" ;

let assert_string_failure = ([res,expected] : [test_exec_result, string]) : unit => {
  let expected = Test.compile_value (expected) ;
  match (res, {
    Fail: (x: test_exec_error) => (
      match (x, {
        Rejected: (x:[michelson_code,address]) => assert (Test.michelson_equal (x[0], expected)),
        Other: (_:unit) => failwith ("contract failed for an unknown reason")
      })),
    Success: (_:unit) => failwith ("bad price check")
  } );
} ;

let _test = (_: unit): unit => {
  /* originate the contract with a initial storage */
  let init_storage = Test.compile_expression (Some (filename),
    (jsligo` Map.literal (list([
      [1 as nat, { current_stock : 50 as nat, max_price : 50 as tez }],
      [2 as nat, { current_stock : 20 as nat, max_price : 75 as tez }] ]))` as ligo_program )
    ) ;
  let [pedro_taco_shop, _code, _size] = Test.originate_from_file (filename, "buy_taco", init_storage, 0 as tez) ;
  /* compile test inputs */
  let classico_kind = Test.compile_value (1 as nat) ;
  let unknown_kind = Test.compile_value (3 as nat) ;

  /* Purchasing a Taco with 1tez and checking that the stock has been updated */
  let ok_case : test_exec_result = Test.transfer (pedro_taco_shop, classico_kind, 1 as tez) ;
  let _u = match (ok_case, {
    Success: (_:unit) => {
      let storage = Test.get_storage_of_address (pedro_taco_shop) ;
      let expected = Test.compile_expression (Some (filename),
        (jsligo` Map.literal (list([
          [1 as nat, { current_stock : 49 as nat, max_price : 50 as tez }],
          [2 as nat, { current_stock : 20 as nat, max_price : 75 as tez }] ]))` as ligo_program )
        ) ;
      assert (Test.michelson_equal (expected, storage)) },
    Fail: (_: test_exec_error) => failwith ("ok test case failed")
  }) ;

  /* Purchasing an unregistred Taco */
  let nok_unknown_kind = Test.transfer (pedro_taco_shop, unknown_kind, 1 as tez) ;
  let _u = assert_string_failure (nok_unknown_kind, "Unknown kind of taco") ;

  /* Attempting to Purchase a Taco with 2tez */
  let nok_wrong_price = Test.transfer (pedro_taco_shop, classico_kind, 2 as tez) ;
  let _u = assert_string_failure (nok_wrong_price, "Sorry, the taco you are trying to purchase has a different price") ;
  return unit
}

let test = _test (unit)
