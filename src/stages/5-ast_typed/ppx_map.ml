open Ppxlib
open Simple_utils

let map (s : string) : string option =
  let s = s |> String.to_seq |> Stdlib.List.of_seq in
  let s = List.rev s in
  let rec g acc xs = match xs with
    | [] -> None
    | '_' :: _xs -> Some acc
    | a :: xs -> g (a :: acc) xs in
  g [] s |> Stdlib.Option.map Stdlib.List.to_seq |> Stdlib.Option.map String.of_seq

let rec extract_ident = function
  Lident id -> id
| Ldot (_, id) -> id
| Lapply (_, lid) -> extract_ident lid

let extract_payload = function
  | PStr [{ pstr_desc =
              Pstr_eval ({ pexp_desc =
                             Pexp_tuple [
                                 { pexp_desc = Pexp_ident id ; _ } ;
                                 { pexp_desc = Pexp_tuple l ; _ }
                               ]
                  ; _ }, _)
          ; _ }] ->
     let f = function
       | { pexp_desc = Pexp_ident id ; _ } -> Some (extract_ident id.txt)
       | _ -> None in
     let l = List.filter_map l ~f in
     Some (extract_ident id.txt, l)
  | _ -> None

let replace_exprs id new_id =
  let replace l = Str.global_replace (Str.regexp_string id) new_id l in
  object
    inherit Ast_traverse.map as super


    method! pattern_desc pat =
      let pat = super#pattern_desc pat in
      match pat with
      | Ppat_var v -> Ppat_var {v with txt = replace v.txt }
      | _ -> pat

    method! longident lid =
      let lid = super#longident lid in
      let rec aux = function
        | Lident l -> Lident (replace l)
        | Ldot (lid, label) -> Ldot (aux lid, replace label)
        | Lapply (lid, lid') -> Lapply (aux lid, aux lid') in
      aux lid
  end

let map_exprs = object
  inherit Ast_traverse.map as super

  method! structure_item_desc = function
      Pstr_value (flg, [expr]) ->
       let return v = Pstr_value (flg, v) in
       let loc = expr.pvb_loc in
       ignore loc;
       let expr = super#value_binding expr in
       let attr = List.find_map expr.pvb_attributes
                    ~f:(function | {attr_name; attr_payload; _} when String.equal attr_name.txt "map" -> Some attr_payload
                                 | _ -> None) in
       let attr = Option.bind attr ~f:extract_payload in
       return @@ Option.value_map attr ~default:[expr] ~f:(fun (id, ids) ->
                     List.map ids ~f:(fun new_id -> (replace_exprs id new_id)#value_binding expr))
    | sid -> sid

end

let () =
  Ppxlib.Driver.register_transformation
    "ppx_map"
    ~impl:map_exprs#structure
    ~intf:map_exprs#signature
